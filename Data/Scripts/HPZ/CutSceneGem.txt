// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: CutScene Gem Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

// Player Aliases
private alias object.animation : player.animation
private alias object.gravity : player.gravity
private alias object.yvel : player.yvel


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.propertyValue
	case 0
		object.inkEffect = INK_ALPHA
		temp0 = oscillation
		temp0 <<= 4
		Sin(temp1, temp0)
		temp1 >>= 5
		temp1 += 224
		object.alpha = temp1
		
		// Cycle through all players
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			
			// Is the player rolling?
			if player[currentPlayer].animation == ANI_JUMPING
				
				// Check if the gem should be broken
				BoxCollisionTest(C_BOX, object.entityPos, -16, -16, 16, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
				if checkResult == true
					
					// Bounce the player up
					player[currentPlayer].yvel = -0x30000
					player[currentPlayer].gravity = GRAVITY_AIR
					
					// Now, break the gem into fragments
					
					CreateTempObject(TypeName[CutScene Gem], 2, object.xpos, object.ypos)
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].speed = 0x2800
					
					CreateTempObject(TypeName[CutScene Gem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 8
					object[tempObjectPos].xvel = -0xC000
					object[tempObjectPos].yvel = -0x20000
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].speed = 0x2800
					
					CreateTempObject(TypeName[CutScene Gem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 8
					object[tempObjectPos].xvel = 0xC000
					object[tempObjectPos].yvel = -0x20000
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].speed = 0x2800
					
					CreateTempObject(TypeName[CutScene Gem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 16
					object[tempObjectPos].xvel = 0x18000
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].speed = 0x2800
					
					CreateTempObject(TypeName[CutScene Gem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 16
					object[tempObjectPos].xvel = -0x18000
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].speed = 0x2800
					
					// Erase this object
					object.type = TypeName[Blank Object]
					
					PlaySfx(SfxName[Block Break], false)
				end if
			else
				// Simply interact with the player as a box
				
				BoxCollisionTest(C_BOX, object.entityPos, -24, -16, 24, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			end if
		next
		break
		
	case 2
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += object.speed
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
		break
		
	end switch
end event


event ObjectDraw
	DrawSpriteFX(object.propertyValue, FX_INK, object.xpos, object.ypos)

	if object.propertyValue == 0
		DrawSprite(1)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("HPZ/Objects.gif")
	
	// Gem frames
	SpriteFrame(-32, -16, 64, 32, 1, 223)
	SpriteFrame(-32, 1, 64, 15, 115, 207)
	
	// Shard frames
	SpriteFrame(-8, -16, 16, 32, 99, 123)
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	object.inkEffect = INK_BLEND
	DrawSpriteFX(0, FX_INK, object.xpos, object.ypos)
	DrawSprite(1)
end event


event RSDKLoad
	LoadSpriteSheet("HPZ/Objects.gif")
	SpriteFrame(-32, -16, 64, 32, 1, 223)
	SpriteFrame(-32, 1, 64, 15, 115, 207)
	
	// just don't mess with prop val, alright?
	// setting it to an unexpected value may cause some unexpected weirdness...
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
