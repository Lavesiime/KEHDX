// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Break Gem Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias 0 : CUTSCENEGEM_INTACT
private alias 1 : CUTSCENEGEM_SHARD

private alias object.value1 : object.gravityStrength

private alias object.animation : player.animation
private alias object.gravity : player.gravity
private alias object.yvel : player.yvel
private alias object.value19 : player.enemyChainCounter


// ========================
// Events
// ========================

event ObjectUpdate

	switch object.state
	case CUTSCENEGEM_INTACT
		object.inkEffect = INK_ALPHA
		temp0 = oscillation
		temp0 <<= 4
		Sin(temp1, temp0)
		temp1 >>= 5
		temp1 += 224
		object.alpha = temp1

		// Cycle through all players
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)

			// Is the player rolling?
			if player[currentPlayer].animation == ANI_JUMPING

				// Check if the gem should be broken
				BoxCollisionTest(C_BOX, object.entityPos, -16, -16, 16, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
				if checkResult == true

					// Player has rolled into gem, break the gem into fragments
					player[currentPlayer].yvel = -0x30000
					player[currentPlayer].gravity = GRAVITY_AIR

					CreateTempObject(TypeName[CutSceneGem], 2, object.xpos, object.ypos)
					object[tempObjectPos].xvel = 0
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].state = CUTSCENEGEM_SHARD
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].gravityStrength = 0x2800

					CreateTempObject(TypeName[CutSceneGem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 8
					object[tempObjectPos].xvel = -0xC000
					object[tempObjectPos].yvel = -0x20000
					object[tempObjectPos].state = CUTSCENEGEM_SHARD
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].gravityStrength = 0x2800
					
					CreateTempObject(TypeName[CutSceneGem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 8
					object[tempObjectPos].xvel = 0xC000
					object[tempObjectPos].yvel = -0x20000
					object[tempObjectPos].state = CUTSCENEGEM_SHARD
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].gravityStrength = 0x2800
					
					CreateTempObject(TypeName[CutSceneGem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos += 16
					object[tempObjectPos].xvel = 0x18000
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].state = CUTSCENEGEM_SHARD
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].gravityStrength = 0x2800
					
					CreateTempObject(TypeName[CutSceneGem], 2, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 16
					object[tempObjectPos].xvel = -0x18000
					object[tempObjectPos].yvel = -0x1C000
					object[tempObjectPos].state = CUTSCENEGEM_SHARD
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].gravityStrength = 0x2800
					
					// Erase this object
					object.type = TypeName[Blank Object]

					switch player[currentPlayer].enemyChainCounter
					case 0
						player.score += 100
						break

					case 1
						player.score += 200
						break

					case 2
						player.score += 500
						break

					case 3
					case 4
					case 5
					case 6
					case 7
					case 8
					case 9
					case 10
					case 11
					case 12
					case 13
					case 14
						player.score += 1000
						break

					case 15
						player.score += 10000
						break

					end switch

					CreateTempObject(TypeName[Object Score], player[currentPlayer].enemyChainCounter, object.xpos, object.ypos)

					object[tempObjectPos].drawOrder = 5

					if player[currentPlayer].gravity == GRAVITY_AIR
						if player[currentPlayer].enemyChainCounter < 15
							player[currentPlayer].enemyChainCounter++
						end if
					end if

					PlaySfx(SfxName[Block Break], 0)
				end if
			else
				// Simply interact with the player as a box
				
				BoxCollisionTest(C_BOX, object.entityPos, -24, -16, 24, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			end if
		next
		break

	case CUTSCENEGEM_SHARD
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += object.gravityStrength
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
		break

	end switch

end event


event ObjectDraw

	if object.state == 0
		DrawSpriteFX(0, FX_INK, object.xpos, object.ypos)
		DrawSprite(1)
	else
		DrawSprite(object.propertyValue)
	end if

end event


event ObjectStartup

	LoadSpriteSheet("HPZ/Objects.gif")

	// Sprite frames

	// Gem frames
	SpriteFrame(-32, -16, 64, 32, 1, 223)
	SpriteFrame(-32, 1, 64, 15, 115, 207)

	// Shard frames
	SpriteFrame(-8, -16, 16, 32, 99, 123)

end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("HPZ/Objects.gif")
	SpriteFrame(-32, -16, 64, 32, 1, 223)
	SpriteFrame(-32, 1, 64, 15, 115, 207)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
