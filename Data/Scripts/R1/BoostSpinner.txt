//---------------Sonic CD Boost Spinner Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.XOriginPos // Set by Ramp3D
private alias object.value1 : Object.Timer
private alias object.value2 : Object.SpinTimer

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// States
private alias 0 : BOOSTSPINNER_IDLE
private alias 1 : BOOSTSPINNER_SPIN

event ObjectUpdate
	if object.state == BOOSTSPINNER_IDLE
		if object.frame == 3
			BoxCollisionTest(C_TOUCH, object.entityPos, 0, -24, 128, 8, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
			if checkResult == true
				object.state = BOOSTSPINNER_SPIN
				Object.SpinTimer = 4
			end if
		end if
	end if
end event


event ObjectDraw
	temp0 = Object.XOriginPos
	temp0 -= object.xpos
	temp0 >>= 20

	object.frame = temp0
	if object.frame > 3
		object.frame = 3
	end if

	if object.state == BOOSTSPINNER_SPIN
		DrawSpriteXY(Object.SpinTimer, Object.XOriginPos, object.ypos)
		Object.Timer++
		if Object.Timer == 3
			Object.Timer = 0

			Object.SpinTimer++
			if Object.SpinTimer == 16
				object.state = BOOSTSPINNER_IDLE
			end if
		end if
	else
		DrawSpriteXY(object.frame, Object.XOriginPos, object.ypos)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-24, -24, 48, 48, 1, 82) // #0  - Boost spinner front
	SpriteFrame(-34, -24, 56, 48, 50, 82) // #1  - Boost spinner angle 1
	SpriteFrame(-38, -24, 57, 48, 107, 82) // #2  - Boost spinner angle 2
	SpriteFrame(-40, -24, 56, 48, 1, 131) // #3  - Boost spinner angle 3
	// Spinning
	SpriteFrame(-40, -24, 56, 48, 58, 131) // #4  - Boost spinner rotating 0
	SpriteFrame(-40, -24, 64, 48, 58, 180) // #5  - Boost spinner rotating 1
	SpriteFrame(-40, -24, 56, 48, 1, 180) // #6  - Boost spinner rotating 2
	SpriteFrame(-40, -24, 64, 48, 123, 180) // #7  - Boost spinner rotating 3
	SpriteFrame(-40, -24, 56, 48, 58, 131) // #8  - Boost spinner rotating 4
	SpriteFrame(-40, -24, 64, 48, 58, 180) // #9  - Boost spinner rotating 5
	SpriteFrame(-40, -24, 56, 48, 1, 180) // #10 - Boost spinner rotating 6
	SpriteFrame(-40, -24, 64, 48, 123, 180) // #11 - Boost spinner rotating 7
	SpriteFrame(-40, -24, 56, 48, 58, 131) // #12 - Boost spinner rotating 8
	SpriteFrame(-40, -24, 64, 48, 58, 180) // #13 - Boost spinner rotating 9
	SpriteFrame(-40, -24, 56, 48, 1, 180) // #14 - Boost spinner rotating 10
	SpriteFrame(-40, -24, 64, 48, 123, 180) // #15 - Boost spinner rotating 11

	// Used to be below LoadSpriteSheet, moved here for consistency	
	foreach (TypeName[Boost Spinner], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 4
	next
end event


// ========================
// Editor Subs
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-24, -24, 48, 48, 1, 82) // #0  - Boost spinner front


end event
