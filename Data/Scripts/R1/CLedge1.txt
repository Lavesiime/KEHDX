//---------------Sonic CD Crumble Ledge 1 Script--------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.Timer
private alias object.value1 : Object.YVelocity

// States
private alias 0 : CLEDGE1_IDLE
private alias 1 : CLEDGE1_DEBRIS
private alias 2 : CLEDGE1_RESTORE // Unused

// Stage SFX
private alias 0 : SFX_S_CRUMBLE

// Priority
private alias 0 : PRIORITY_BOUNDS

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

event ObjectUpdate
	switch object.state
	case CLEDGE1_DEBRIS
		if Object.Timer > 0
			Object.Timer--
		else
			Object.YVelocity += 0x5000
			object.ypos += Object.YVelocity
		end if
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
		break

	case CLEDGE1_RESTORE

		if object.outOfBounds == true
			object.state = CLEDGE1_IDLE
			object.priority = PRIORITY_BOUNDS
		end if

		break
	end switch
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)

	switch object.propertyValue
	case 0
		if object.state == CLEDGE1_IDLE

			BoxCollisionTest(C_PLATFORM, object.entityPos, -40, -24, 40, -8, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)

			if checkResult == true

				object.priority = PRIORITY_BOUNDS
				object.type = TypeName[Blank Object]
				// TempValue0	:	Debris chunk value
				// TempValue1	:	Ledge section value
				// TempValue2	:	Debris for ledge section
				// TempValue3	:	Fall Timer
				// TempValue4	:	XPos
				// TempValue5	:	YPos
				temp0 = 1
				temp1 = 0
				temp4 = object.xpos
				temp4 -= 0x200000
				while temp1 < 5
					temp2 = 0
					temp5 = object.ypos
					temp5 += 0x200000
					switch temp1
					case 0
						temp3 = 10
						break
					case 1
						temp3 = 22
						break
					case 2
						temp3 = 34
						break
					case 3
						temp3 = 46
						break
					case 4
						temp3 = 58
						break
					end switch

					while temp2 < 5
						CreateTempObject(TypeName[CrumbleLedge1], temp0, temp4, temp5)
						Object.Timer[TempObjectPos] = temp3
						object[TempObjectPos].state = CLEDGE1_DEBRIS

						temp3 += 5
						temp0++
						temp2++
						temp5 -= 0x100000
					loop
					temp3 -= 8
					temp1++
					temp4 += 0x100000
				loop
				PlaySfx(SfxName[Crumble], false)

			end if

		end if
		break
		// The five debris chunks at the very top retains collision
	case 4
	case 9
	case 14
	case 19
	case 24
		if Object.Timer > 0

			BoxCollisionTest(C_PLATFORM, object.entityPos, -8, -8, 8, 8, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)

		end if
		break

	end switch

	next
end event


event ObjectDraw
	if object.state < CLEDGE1_RESTORE
		DrawSprite(object.propertyValue)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-40, -40, 80, 80, 1, 1) // #0 - Ledge

	// Debris chunks
	temp1 = 1
	while temp1 < 80
		temp0 = 65
		while temp0 > 0
			SpriteFrame(-8, -8, 16, 16, temp1, temp0)
			temp0 -= 16
		loop
		temp1 += 16
	loop
end event


// ========================
// Editor Subs
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-40, -40, 80, 80, 1, 1) // #0 - Ledge


end event
