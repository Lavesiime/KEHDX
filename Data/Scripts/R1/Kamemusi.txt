//-----------------Sonic CD Kamemusi Script-------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.Timer
private alias object.value1 : Object.FireTimer

private alias object.propertyValue : Object.Quality

// Bullet Aliases
private alias object.value1 : Bullet.XVelocity
private alias object.value2 : Bullet.YVelocity

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// States
private alias 0 : KAMEMUSI_MOVE_LEFT
private alias 1 : KAMEMUSI_MOVE_RIGHT
private alias 2 : KAMEMUSI_SHOOT

// Collision Sides
private alias 0 : CSIDE_FLOOR

// Stage SFX
private alias 1 : SFX_S_SHOT

// Badnik Quality / Property Values
private alias 0 : GOOD_QUALITY
private alias 1 : BAD_QUALITY


event ObjectUpdate
	if Object.Quality == GOOD_QUALITY
		switch object.state
		case KAMEMUSI_MOVE_LEFT
			object.frame++
			object.frame %= 60

			object.xpos -= 0xAAAA

			Object.Timer--
			if Object.Timer == -192
				object.state = KAMEMUSI_MOVE_RIGHT
				object.direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0) // Check if there's no more floor
			if checkResult == false
				object.state = KAMEMUSI_MOVE_RIGHT
				Object.Timer = -192
				object.direction = FACING_LEFT
			end if

			if Object.FireTimer > -1
				BoxCollisionTest(C_TOUCH, object.entityPos, -64, -64, 64, 64, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
				if checkResult == true
					object.state = KAMEMUSI_SHOOT
					object.drawOrder = 4
				end if
			else
				Object.FireTimer++
			end if
			break

		case KAMEMUSI_MOVE_RIGHT
			object.frame++
			object.frame %= 60

			object.xpos += 0xAAAA

			Object.Timer++
			if Object.Timer == 192
				object.state = KAMEMUSI_MOVE_LEFT
				object.direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0) // Check if there's no more floor
			if checkResult == false
				object.state = KAMEMUSI_MOVE_LEFT
				Object.Timer = 192
				object.direction = FACING_RIGHT
			end if

			if Object.FireTimer > -1
				BoxCollisionTest(C_TOUCH, object.entityPos, -64, -64, 64, 64, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)

				if checkResult == true
					object.state = KAMEMUSI_SHOOT
					object.drawOrder = 4
				end if
			else
				Object.FireTimer++
			end if
			break

		case KAMEMUSI_SHOOT
			if Object.FireTimer == 60
				CreateTempObject(TypeName[Bullet], 0, object.xpos, object.ypos)
				Bullet.XVelocity[TempObjectPos] = -0x20000
				Bullet.YVelocity[TempObjectPos] = -0x50000
			end if

			if Object.FireTimer == 62
				CreateTempObject(TypeName[Bullet], 0, object.xpos, object.ypos)
				Bullet.XVelocity[TempObjectPos] = 0x20000
				Bullet.YVelocity[TempObjectPos] = -0x50000
				PlaySfx(SfxName[Shot], false)
			end if

			if Object.FireTimer == 64
				object.state = object.direction
				Object.FireTimer = -120
				object.drawOrder = 3
			else
				Object.FireTimer++
			end if

			break
		end switch
	else
		switch object.state
		case KAMEMUSI_MOVE_LEFT
			object.frame++
			object.frame %= 12

			object.xpos -= 0x3800

			Object.Timer--
			if Object.Timer == -384
				object.state = KAMEMUSI_MOVE_RIGHT
				object.direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0)
			if checkResult == false
				object.state = KAMEMUSI_MOVE_RIGHT
				Object.Timer = -384
				object.direction = FACING_LEFT
			end if

			if Object.FireTimer == 82
				object.state = KAMEMUSI_SHOOT
				Object.FireTimer = 0
			else
				Object.FireTimer++
			end if

			break

		case KAMEMUSI_MOVE_RIGHT
			object.frame++
			object.frame %= 12

			object.xpos += 0x3800
			Object.Timer++
			if Object.Timer == 384
				object.state = KAMEMUSI_MOVE_LEFT
				object.direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 14, 0)
			if checkResult == false
				object.state = KAMEMUSI_MOVE_LEFT
				Object.Timer = 384
				object.direction = FACING_RIGHT
			end if

			if Object.FireTimer == 82
				object.state = KAMEMUSI_SHOOT
				Object.FireTimer = 0
			else
				Object.FireTimer++
			end if
			break

		case KAMEMUSI_SHOOT // in his broken state, the shoot will fail
			if Object.FireTimer == 14
				object.state = object.direction
				Object.FireTimer = 0
			else
				Object.FireTimer++
			end if
			break

		end switch
	end if

	BoxCollisionTest(C_TOUCH, object.entityPos, -16, -14, 16, 14, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
	if checkResult == true
		CallFunction(Player_BadnikBreak)
		if object.type == TypeName[Blank Object]	
			CallFunction(EmeraldManager_AwardEmerald)	
		end if
	end if
end event


event ObjectDraw
	if Object.Quality == GOOD_QUALITY
		if object.state < KAMEMUSI_SHOOT
			temp0 = object.frame
			temp0 /= 30
			DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		else
			DrawSpriteFX(4, FX_FLIP, object.xpos, object.ypos)
		end if
	else
		if object.state < KAMEMUSI_SHOOT
			temp0 = object.frame
			temp0 /= 6
			temp0 += 2
			DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
		else
			DrawSpriteFX(4, FX_FLIP, object.xpos, object.ypos)
		end if
	end if
end event


event ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	// Good Quality
	SpriteFrame(-16, -15, 32, 29, 223, 34) // #0 - Kamemusi Movement Frame 0
	SpriteFrame(-16, -14, 32, 28, 223, 93) // #1 - Kamemusi Movement Frame 1
	// Bad Quality
	SpriteFrame(-16, -15, 32, 29, 223, 34) // #2 - Old Kamemusi Movement Frame 0
	SpriteFrame(-16, -14, 32, 28, 223, 64) // #3 - Old Kamemusi Movement Frame 1

	SpriteFrame(-16, -17, 32, 31, 223, 122) // #4 - Kamemusi Shoot Position

	foreach (TypeName[Kamemusi], arrayPos0, ALL_ENTITIES)
		CallFunction(EmeraldManager_AddEntity)
	next
end event


// ========================
// Editor Subs
// ========================




// Property Value



// type






// Property Value



// type








event RSDKDraw
	DrawSprite(Object.Quality)
end event


event RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")
	SpriteFrame(-16, -15, 32, 29, 223, 34) // #0 - Kamemusi Movement Frame 0
	SpriteFrame(-16, -15, 32, 29, 223, 34) // #0 - Old Kamemusi Movement Frame 0






end event
