//-------------------Sonic CD Anton Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.Timer

private alias object.propertyValue : Object.Quality

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// States
private alias 0 : ANTON_MOVING_LEFT
private alias 1 : ANTON_MOVING_RIGHT

// Collision Sides
private alias 0 : CSIDE_FLOOR

// Badnik Quality / Property Values
private alias 0 : GOOD_QUALITY
private alias 1 : BAD_QUALITY

//Anton more like Spamton am I right?

event ObjectUpdate
	if Object.Quality == GOOD_QUALITY

		object.frame += 2
		switch object.state
		case ANTON_MOVING_LEFT
			object.xpos -= 0x10000

			Object.Timer--
			if Object.Timer == -128
				object.state = ANTON_MOVING_RIGHT
				object.direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if checkResult == false
				object.state = ANTON_MOVING_RIGHT
				Object.Timer = -128
				object.direction = FACING_LEFT
			end if
			break

		case ANTON_MOVING_RIGHT
			object.xpos += 0x10000

			Object.Timer++
			if Object.Timer == 128
				object.state = ANTON_MOVING_LEFT
				object.direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if checkResult == false
				object.state = ANTON_MOVING_LEFT
				Object.Timer = 128
				object.direction = FACING_RIGHT
			end if
			break

		end switch

	else

		object.frame++
		switch object.state
		case ANTON_MOVING_LEFT
			object.xpos -= 0x8000

			Object.Timer--
			if Object.Timer == -256
				object.state = ANTON_MOVING_RIGHT
				object.direction = FACING_LEFT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if checkResult == false
				object.state = ANTON_MOVING_RIGHT
				Object.Timer = -256
				object.direction = FACING_LEFT
			end if
			break

		case ANTON_MOVING_RIGHT
			object.xpos += 0x8000

			Object.Timer++
			if Object.Timer == 256
				object.state = ANTON_MOVING_LEFT
				object.direction = FACING_RIGHT
			end if

			ObjectTileGrip(CSIDE_FLOOR, 0, 20, 0)
			if checkResult == false
				object.state = ANTON_MOVING_LEFT
				Object.Timer = 256
				object.direction = FACING_RIGHT
			end if
			break

		end switch

	end if

	object.frame &= 15

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -12, -18, 12, 20, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
		if checkResult == true
			CallFunction(Player_BadnikBreak)
			if object.type == TypeName[Blank Object]	
				CallFunction(EmeraldManager_AwardEmerald)	
			end if
		end if
	next
end event


event ObjectDraw
	temp0 = object.frame
	temp0 >>= 3
	if Object.Quality == GOOD_QUALITY
		temp0 += 4
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos) // Wheel
		temp0 -= 4
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos) // Anton
		temp0 += 6
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos) // Smoke Puff
	else
		temp0 += 4
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos) // Wheel
		temp0 -= 2
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos) // Anton
		temp0 += 4
		DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos) // Smoke Puff
	end if
end event


event ObjectStartup
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-12, -19, 24, 32, 1, 1) // #0 - Anton Frame 0
	SpriteFrame(-12, -20, 24, 32, 1, 1) // #1 - Anton Frame 1
	SpriteFrame(-12, -19, 24, 32, 26, 1) // #2 - Old Anton Frame 0
	SpriteFrame(-12, -20, 24, 32, 26, 1) // #3 - Old Anton Frame 1

	SpriteFrame(-8, 4, 16, 16, 1, 34) // #4 - Anton Wheel Frame 0
	SpriteFrame(-8, 4, 16, 16, 18, 34) // #5 - Anton Wheel Frame 1

	SpriteFrame(14, -4, 8, 8, 35, 34) // #6 - Anton Smoke Puff Frame 0
	SpriteFrame(18, -5, 8, 8, 35, 34) // #7 - Anton Smoke Puff Frame 1

	foreach (TypeName[Anton], arrayPos0, ALL_ENTITIES)
		CallFunction(EmeraldManager_AddEntity)
	next
end event


// ========================
// Editor Subs
// ========================




// Property Value



// type






// Property Value



// type








event RSDKDraw
	// Wheel
	object.frame = Object.Quality
	object.frame += 2
	DrawSprite(object.frame)

	// Anton
	DrawSprite(Object.Quality)
end event


event RSDKLoad
	LoadSpriteSheet("R1/Objects.gif")

	SpriteFrame(-12, -19, 24, 32, 1, 1) // #0 - Anton Frame 0
	SpriteFrame(-12, -19, 24, 32, 26, 1) // #1 - Old Anton Frame 0

	SpriteFrame(-8, 4, 16, 16, 1, 34) // #2 - Anton Wheel Frame 0
	SpriteFrame(-8, 4, 16, 16, 18, 34) // #3 - Anton Wheel Frame 1






end event
