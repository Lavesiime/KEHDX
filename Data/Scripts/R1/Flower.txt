//------------------Sonic CD Flower Script--------------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : Object.Timer

// States
private alias 0 : FLOWER_SEED_SPAWN
private alias 1 : FLOWER_SEED_FALL

private alias 2 : FLOWER_SEED_PLANTED_1
private alias 3 : FLOWER_SEED_PLANTED_2
private alias 4 : FLOWER_SEED_PLANTED_3

private alias 5 : FLOWER_ROOT_GROWTH_1
private alias 6 : FLOWER_ROOT_GROWTH_2

private alias 7 : FLOWER_ANIM_1
private alias 8 : FLOWER_ANIM_2
private alias 9 : FLOWER_ANIM_3

// Collision Sides
private alias 0 : CSIDE_FLOOR


event ObjectUpdate
	switch object.state
	case FLOWER_SEED_SPAWN
		object.frame = 8
		Object.Timer++
		if Object.Timer == 20
			Object.Timer = 0
			object.state++
		end if
		break

	case FLOWER_SEED_FALL
		object.frame = Object.Timer
		object.frame >>= 2

		Object.Timer++
		Object.Timer &= 7

		object.ypos += 0x20000
		ObjectTileCollision(CSIDE_FLOOR, 0, 8, 0)
		if checkResult == true
			object.ypos += 0x80000
			object.state++
			Object.Timer = 0
			object.frame = 2
		end if
		break

	case FLOWER_SEED_PLANTED_1
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			object.state++
			object.frame = 3
		end if
		break

	case FLOWER_SEED_PLANTED_2
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			object.state++
			object.frame = 2
		end if
		break

	case FLOWER_SEED_PLANTED_3
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			object.state++
			object.frame = 3
		end if
		break

	case FLOWER_ROOT_GROWTH_1
		Object.Timer++
		if Object.Timer == 5
			Object.Timer = 0
			object.state++
			object.frame = 4
		end if
		break

	case FLOWER_ROOT_GROWTH_2
		Object.Timer++
		if Object.Timer == 4
			Object.Timer = 0
			object.state++
			object.frame = 5
		end if
		break

	case FLOWER_ANIM_1
		Object.Timer++
		if Object.Timer == 2
			Object.Timer = 0
			object.state++
			object.frame = 6
		end if
		break

	case FLOWER_ANIM_2
		Object.Timer++
		if Object.Timer == 20
			Object.Timer = 0
			object.state++
			object.frame = 7
		end if
		break

	case FLOWER_ANIM_3
		Object.Timer++
		if Object.Timer == 20
			Object.Timer = 0
			object.state--
			object.frame = 6
		end if
		break

	end switch
end event


event ObjectDraw
	DrawSprite(object.frame)
end event


event ObjectStartup
	LoadSpriteSheet("R1/Objects2.gif")

	SpriteFrame(-4, -8, 8, 16, 180, 1) // #0 - Seed Frame 0
	SpriteFrame(-4, -8, 8, 16, 180, 18) // #1 - Seed Frame 1

	SpriteFrame(-8, -16, 16, 16, 26, 229) // #2 - Planted Seed Frame 0
	SpriteFrame(-8, -16, 16, 16, 43, 229) // #3 - Planted Seed Frame 1

	SpriteFrame(-8, -16, 16, 16, 60, 229) // #4 - Flower Growth Frame 0
	SpriteFrame(-12, -24, 24, 24, 165, 90) // #5 - Flower Growth Frame 1

	SpriteFrame(-12, -48, 24, 48, 165, 66) // #6 - Flower Frame 0
	SpriteFrame(-12, -48, 24, 48, 165, 115) // #7 - Flower Frame 1

	SpriteFrame(0, 0, 0, 0, 0, 0) // #8 - Cheat Sprite
end event


// ========================
// Editor Subs
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("R1/Objects2.gif")
	SpriteFrame(-12, -48, 24, 48, 165, 66) // #6 - Flower Frame 0


end event
