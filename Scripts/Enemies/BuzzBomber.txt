// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Buzz Bomber Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0 : buzzBomber.timer
private alias object.value1 : buzzBomber.startX
private alias object.value2 : buzzBomber.hasShot
private alias object.value3 : buzzBomber.hideOnOffScreen // idk if this is ever actually used

private alias object.value38 : player.hitboxLeft
private alias object.value39 : player.hitboxRight
private alias object.value40 : player.hitboxTop
private alias object.value41 : player.hitboxBottom

private alias 0 : BUZZBOMBER_BUZZAROUND
private alias 1 : BUZZBOMBER_IDLEDELAY
private alias 2 : BUZZBOMBER_FOUNDPLAYER
private alias 3 : BUZZBOMBER_SHOOTING
private alias 4 : BUZZBOMBER_HIDDEN

private alias 0 : BUZZBOMBER_ANI_FLY
private alias 1 : BUZZBOMBER_ANI_FLY_IDLE
private alias 2 : BUZZBOMBER_ANI_SHOOT
private alias 3 : BUZZBOMBER_ANI_NONE

// Function declarations
reserve function BuzzBomber_DebugDraw
reserve function BuzzBomber_DebugSpawn

function BuzzBomber_DebugDraw
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
	DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
end function


function BuzzBomber_DebugSpawn
	CreateTempObject(TypeName[Buzz Bomber], 0, object.xpos, object.ypos)

	// Get the X part of object direction
	GetBit(temp0, object.direction, 0)

	if temp0 == false
		// Facing left
		object[tempObjectPos].direction = FLIP_NONE
		object[tempObjectPos].xvel 		= -0x40000
	else
		// Facing right
		object[tempObjectPos].direction = FLIP_X
		object[tempObjectPos].xvel 		= 0x40000
	end if
	
end function


event ObjectMain
	switch object.state
	case BUZZBOMBER_BUZZAROUND
		object.priority = PRIORITY_ACTIVE
		if buzzBomber.timer < 127
			buzzBomber.timer++
			object.xpos += object.xvel
		else
			buzzBomber.timer 	= 0
			object.state 		= BUZZBOMBER_IDLEDELAY
			object.animation 	= BUZZBOMBER_ANI_FLY_IDLE
			object.direction 	^= FLIP_X
			FlipSign(object.xvel)
		end if
		break

	case BUZZBOMBER_IDLEDELAY
		if buzzBomber.timer < 59
			buzzBomber.timer++
		else
			buzzBomber.timer 	= 0
			object.state 		= BUZZBOMBER_BUZZAROUND
			object.animation 	= BUZZBOMBER_ANI_FLY
			buzzBomber.hasShot	= false
		end if
		break

	case BUZZBOMBER_FOUNDPLAYER
		if buzzBomber.timer < 29
			buzzBomber.timer++
		else
			buzzBomber.timer = 0
			object.state++
			object.animation = BUZZBOMBER_ANI_SHOOT
		end if
		break

	case BUZZBOMBER_SHOOTING
		if buzzBomber.timer < 59
			buzzBomber.timer++
			if buzzBomber.timer == 15
				CreateTempObject(TypeName[Buzz Bomber Shot], 0, object.xpos, object.ypos)
				object[tempObjectPos].direction = object.direction
				if object.direction == FLIP_NONE
					object[tempObjectPos].xpos -= 0x180000
					object[tempObjectPos].xvel = -0x20000
				else
					object[tempObjectPos].xpos += 0x180000
					object[tempObjectPos].xvel = 0x20000
				end if
				object[tempObjectPos].ypos += 0x1C0000
				object[tempObjectPos].yvel = 0x20000
			end if
		else
			buzzBomber.timer	= 0
			object.state 		= BUZZBOMBER_BUZZAROUND
			object.animation 	= BUZZBOMBER_ANI_FLY
			buzzBomber.hasShot	= true
		end if
		break

	case BUZZBOMBER_HIDDEN
		object.animation = BUZZBOMBER_ANI_NONE
		break
	end switch

	if object.outOfBounds == true
		temp0 		= object.xpos
		object.xpos = buzzBomber.startX
		if buzzBomber.hideOnOffScreen == true
			object.state = BUZZBOMBER_HIDDEN
		end if

		if object.outOfBounds == true
			object.xpos = buzzBomber.startX

			GetBit(temp0, object.propertyValue, 0)
			if temp0 == 0
				object.direction = FLIP_NONE
				object.xvel 	 = -0x40000
			else
				object.direction = FLIP_X
				object.xvel 	 = 0x40000
			end if
			buzzBomber.timer	= 0
			object.animation 	= BUZZBOMBER_ANI_FLY
			object.priority 	= PRIORITY_ACTIVE_BOUNDS
			object.state 		= BUZZBOMBER_BUZZAROUND
		else
			object.xpos = temp0
		end if
	end if

	//Player Collisons
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		if object.state == BUZZBOMBER_BUZZAROUND
			if buzzBomber.hasShot == false
				if object.propertyValue < 2
					BoxCollisionTest(C_TOUCH, object.entityPos, -96, -256, 96, 256, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				else
					BoxCollisionTest(C_TOUCH, object.entityPos, -48, -256, 48, 256, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
				end if
				if checkResult == true
					object.state 		= BUZZBOMBER_FOUNDPLAYER
					buzzBomber.timer 	= 0
					object.animation 	= BUZZBOMBER_ANI_FLY_IDLE
				end if
			end if
		end if
		if object.state < BUZZBOMBER_HIDDEN
			BoxCollisionTest(C_TOUCH, object.entityPos, -24, -12, 24, 12, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
			if checkResult == true
				CallFunction(PlayerObject_BadnikBreak)
				if object.type == TypeName[Blank Object]
					CallFunction(EmeraldManager_AwardEmerald)
				end if
			end if
		end if
	next

	object.frame = object.animationTimer
	object.frame >>= 1
	object.frame += 2
	object.animationTimer++
	object.animationTimer &= 3
end event


event ObjectDraw
	if object.state < BUZZBOMBER_HIDDEN
		if object.direction == FLIP_NONE
			DrawSpriteFX(object.frame, FX_INK, object.xpos, object.ypos)
		else
			object.frame += 2
			DrawSpriteFX(object.frame, FX_INK, object.xpos, object.ypos)
			object.frame -= 2
		end if
	end if

	switch object.animation
	case BUZZBOMBER_ANI_FLY
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		object.frame += 4
		DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
		object.frame -= 4
		break

	case BUZZBOMBER_ANI_FLY_IDLE
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case BUZZBOMBER_ANI_SHOOT
		DrawSpriteFX(1, FX_FLIP, object.xpos, object.ypos)
		break

	default
		break
	end switch
end event


event ObjectStartup
	CheckCurrentStageFolder("GHZ")
	if checkResult == true
		LoadSpriteSheet("GHZ/Objects.gif")
		SpriteFrame(-23, -9, 45, 19, 98, 74)
		SpriteFrame(-23, -9, 36, 29, 149, 49)
		SpriteFrame(-17, -15, 35, 8, 144, 79)
		SpriteFrame(-18, -10, 37, 5, 144, 88)
		SpriteFrame(-18, -15, 35, 8, 145, 220)
		SpriteFrame(-19, -10, 37, 5, 143, 229)
		SpriteFrame(13, 4, 6, 5, 98, 68)
		SpriteFrame(13, 4, 10, 5, 104, 68)
	end if

	CheckCurrentStageFolder("Zone02")
	if checkResult == true
		LoadSpriteSheet("MZ/Objects.gif")
		SpriteFrame(-23, -9, 45, 19, 1, 127)
		SpriteFrame(-23, -9, 36, 29, 1, 147)
		SpriteFrame(-17, -15, 35, 8, 38, 147)
		SpriteFrame(-18, -10, 37, 5, 38, 156)
		SpriteFrame(-18, -15, 35, 8, 38, 162)
		SpriteFrame(-19, -10, 37, 5, 38, 171)
		SpriteFrame(13, 4, 6, 5, 76, 153)
		SpriteFrame(13, 4, 10, 5, 74, 147)
	end if

	CheckCurrentStageFolder("Zone03")
	if checkResult == true
		LoadSpriteSheet("SYZ/Objects.gif")
		SpriteFrame(-23, -9, 45, 19, 1, 81)
		SpriteFrame(-23, -9, 36, 29, 1, 101)
		SpriteFrame(-17, -15, 35, 8, 38, 101)
		SpriteFrame(-18, -10, 37, 5, 38, 110)
		SpriteFrame(-18, -15, 35, 8, 38, 116)
		SpriteFrame(-19, -10, 37, 5, 38, 125)
		SpriteFrame(13, 4, 6, 5, 76, 123)
		SpriteFrame(13, 4, 10, 5, 83, 123)
	end if

	CheckCurrentStageFolder("Zone07")
	if checkResult == true
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-23, -9, 45, 19, 1, 1)
		SpriteFrame(-23, -9, 36, 29, 1, 21)
		SpriteFrame(-17, -15, 35, 8, 38, 21)
		SpriteFrame(-18, -10, 37, 5, 38, 30)
		SpriteFrame(-18, -15, 35, 8, 38, 36)
		SpriteFrame(-19, -10, 37, 5, 38, 45)
		SpriteFrame(13, 4, 6, 5, 47, 15)
		SpriteFrame(13, 4, 10, 5, 54, 15)
	end if

	// propertyValue info:
	// bit 0 = direction
	// bit 1 = range type (large/small)

	foreach (TypeName[Buzz Bomber], arrayPos0, ALL_ENTITIES)
		buzzBomber[arrayPos0].startX	= object[arrayPos0].xpos
		object[arrayPos0].inkEffect 	= INK_ALPHA
		object[arrayPos0].alpha 		= 0xC0
		GetBit(temp0, object[arrayPos0].propertyValue, 0)
		if temp0 == 0
			object[arrayPos0].direction = FLIP_NONE
			object[arrayPos0].xvel 		= -0x40000
		else
			object[arrayPos0].direction = FLIP_X
			object[arrayPos0].xvel 		= 0x40000
		end if
		CallFunction(EmeraldManager_AddEntity)
	next

	SetTableValue(TypeName[Buzz Bomber], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(BuzzBomber_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(BuzzBomber_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
