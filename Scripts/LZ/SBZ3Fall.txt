// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: SBZ3 Fall Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias 72 : TYPE_SBZ3FALL

private alias 0 : STATE_STAGE	// Version of the object placed in a stage
private alias 1 : STATE_PLAYER	// Version of the object spawned for a player

event ObjectMain

	if object.state == 0
		// Create an SBZ3 Fall object for every player
		
		currentPlayer = 0
		while currentPlayer < playerCount
			CreateTempObject(TypeName[SBZ3 Fall], 0, object.xpos, object.ypos)
			object[tempObjectPos].state = 1
			object[tempObjectPos].value0 = currentPlayer
			object[tempObjectPos].direction = FLIP_X
			object[tempObjectPos].animation = ANI_HURT

			currentPlayer++
		loop

		object.type = TypeName[Blank Object]
	else
		currentPlayer = object.value0

		if object[currentPlayer].gravity == GRAVITY_GROUND
			object.type = TypeName[Blank Object]
		else
			object[currentPlayer].direction = object.direction
			object[currentPlayer].animation = object.animation

			if object[currentPlayer].animation == ANI_JUMPING
				object[currentPlayer].state = PlayerObject_HandleAir
				object.type = TypeName[Blank Object]
			end if

			if object[currentPlayer].animation == ANI_FLYING
				object[currentPlayer].state = PlayerObject_HandleTailsFlight
				object[currentPlayer].value26 = 0x800
				object.type = TypeName[Blank Object]
			end if

			if object[currentPlayer].animation == ANI_GLIDING
				if object[currentPlayer].direction == FLIP_X
					object[currentPlayer].state = PlayerObject_KnuxGlideLeft
				else
					object[currentPlayer].state = PlayerObject_KnuxGlideRight
				end if

				object.type = TypeName[Blank Object]
			end if

			if object[currentPlayer].animation == ANI_GLIDING_DROP
				object[currentPlayer].state = PlayerObject_KnuxGlideDrop
				object.type = TypeName[Blank Object]
			end if
		end if
	end if
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
