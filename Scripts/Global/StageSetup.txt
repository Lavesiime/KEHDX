// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Stage Setup Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases

private alias object.visible : player.visible
private alias object.value18 : player.sortedDrawOrder
private alias object.value20 : pause.KEH
private alias object.value21 : player.nodraw

// Function declarations
reserve function StageSetup_HandleOscillationTable

// Static Values
public value StageSetup_monitorOverlayFrame = 0;
public value StageSetup_oscillateFlipFlags = 0;

// Tables
public table StageSetup_initOscillationTable
	128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0
	128, 0, 0x3848, 238, 0x2080, 180, 0x3080, 270, 0x5080, 450, 0x7080, 630, 128, 0, 0x4000, 254
end table

public alias StageSetup_oscillationTable : StageSetup_table9

public table StageSetup_oscillationTable
	128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0
	128, 0, 0x3848, 238, 0x2080, 180, 0x3080, 270, 0x5080, 450, 0x7080, 630, 128, 0, 0x4000, 254
end table

public table StageSetupS1_oscillationTable
	128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0, 128, 0
	128, 0, 0x50F0, 286, 0x2080, 180, 0x3080, 270, 0x5080, 450, 0x7080, 630, 128, 0, 128, 0
end table

public table StageSetupS1_oscillateLimits
	2, 0x1000, 2, 0x1800, 2, 0x2000, 2, 0x3000, 4, 0x2000, 8, 0x800,  8, 0x4000, 4, 0x4000
	2, 0x5000, 2, 0x5000, 2, 0x2000, 3, 0x3000, 5, 0x5000, 7, 0x7000, 2, 0x1000, 2, 0x1000
end table

public table StageSetup_oscillateLimits
	2, 0x1000, 2, 0x1800, 2, 0x2000, 2, 0x3000, 4, 0x2000, 8, 0x800,  8, 0x4000, 4, 0x4000
	2, 0x3800, 2, 0x3800, 2, 0x2000, 3, 0x3000, 5, 0x5000, 7, 0x7000, 2, 0x4000, 2, 0x4000
end table


function StageSetup_HandleOscillationTable
	temp0 = 0
	temp1 = 0
	while temp0 < 16
		GetTableValue(temp4, temp1, StageSetup_oscillateLimits)		// temp4 = oscillateSpeed
		GetTableValue(temp6, temp1, StageSetup_oscillationTable)	// temp6 = oscillatePos
		temp1++

		GetTableValue(temp5, temp1, StageSetup_oscillateLimits)		// temp5 = oscillateMax
		GetTableValue(temp7, temp1, StageSetup_oscillationTable)	// temp7 = oscillateInc
		temp1--

		GetBit(temp2, StageSetup_oscillateFlipFlags, temp0)
		if temp2 == false
			temp7 += temp4
			temp6 += temp7
			if temp6 >= temp5
				SetBit(StageSetup_oscillateFlipFlags, temp0, true)
			end if
		else
			temp7 -= temp4
			temp6 += temp7
			if temp6 < temp5
				SetBit(StageSetup_oscillateFlipFlags, temp0, false)
			end if
		end if

		SetTableValue(temp6, temp1, StageSetup_oscillationTable)
		temp1++

		SetTableValue(temp7, temp1, StageSetup_oscillationTable)
		temp1++

		temp0++
	loop
end function


function StageSetup_HandleS1OscillationTable
	temp0 = 0
	temp1 = 0
	while temp0 < 16
		GetTableValue(temp4, temp1, StageSetupS1_oscillateLimits)		// temp4 = oscillateSpeed
		GetTableValue(temp6, temp1, StageSetupS1_oscillationTable)	// temp6 = oscillatePos
		temp1++

		GetTableValue(temp5, temp1, StageSetupS1_oscillateLimits)		// temp5 = oscillateMax
		GetTableValue(temp7, temp1, StageSetupS1_oscillationTable)	// temp7 = oscillateInc
		temp1--

		GetBit(temp2, StageSetup_oscillateFlipFlags, temp0)
		if temp2 == false
			temp7 += temp4
			temp6 += temp7
			if temp6 >= temp5
				SetBit(StageSetup_oscillateFlipFlags, temp0, true)
			end if
		else
			temp7 -= temp4
			temp6 += temp7
			if temp6 < temp5
				SetBit(StageSetup_oscillateFlipFlags, temp0, false)
			end if
		end if

		SetTableValue(temp6, temp1, StageSetupS1_oscillationTable)
		temp1++

		SetTableValue(temp7, temp1, StageSetupS1_oscillationTable)
		temp1++

		temp0++
	loop
end function


event ObjectMain
	switch stage.state
	case STAGE_PAUSED
		foreach (TypeName[Title Card], arrayPos0, ALL_ENTITIES)
			pause.KEH = 0
			return
		next
		switch pause.KEH
		case 0
			pause.KEH = 1
			break
		case 1
			if inputPress.up == 1
				object.value0--
				if object.value0 == -1
					object.value0 = 2
				end if
				PlaySfx(SfxName[Menu Move], 0)
			end if
			if inputPress.down == 1
				object.value0++
				if object.value0 == 3
					object.value0 = 0
				end if
				PlaySfx(SfxName[Menu Move], 0)
			end if
			if inputPress.start == 1
				PlaySfx(SfxName[Menu Back], 0)
				stage.state = STAGE_RUNNING
				ResumeMusic()
			end if
			temp0 = inputPress.buttonA
			temp0 |= inputPress.buttonB
			temp0 |= inputPress.buttonC
			if temp0 == 1
				switch object.value0
				case 0
					PlaySfx(SfxName[Menu Back], 0)
					stage.state = STAGE_RUNNING
					player.nodraw = 0
					ResumeMusic()
					break
				case 1
					pause.KEH = 2
					object.value2 = 0
					PlaySfx(SfxName[Menu Select], 0)
					break
				case 2
					pause.KEH = 3
					object.value2 = 0
					PlaySfx(SfxName[Menu Select], 0)
					break
				end switch
			end if
			break
		case 2
			object.value2 += 4
			if object.value2 == 384
				LoadStage()
				object.value2 = 384
			end if
			break
		case 3
			object.value2 += 4
			if object.value2 == 384
				stage.activeList = PRESENTATION_STAGE
				stage.listPos = 4
				object.value2 = 384
				LoadStage()
			end if
			break
		end switch
		break
	case STAGE_FROZEN
		// Do nothing
		break
	default
		pause.KEH = 0

		// Handle Ring Frame
		ringTimer++
		if ringTimer == 4
			ringTimer = 0
			ringFrame++
			ringFrame &= 7
		end if

		// Handle Monitor overlay frame
		StageSetup_monitorOverlayFrame++
		if StageSetup_monitorOverlayFrame > 17
			StageSetup_monitorOverlayFrame = 0
		end if
		
		// Handle Player Score Bonus
		if options.gameMode != 2
			if player.score >= player.scoreBonus
				player.lives++
				player.scoreBonus += 0xC350
				PlaySfx(SfxName[Life], 0)
				PauseMusic()
				ResetObjectEntity(25, TypeName[Music Event], 2, 0, 0)
				object[25].priority = PRIORITY_ACTIVE
			end if
		end if

		// Handle Oscillations & Platform Array movements
		oscillation++
		oscillation &= 511
		if stage.listPos == 14
			CallFunction(StageSetup_HandleS1OscillationTable)
		else
			CallFunction(StageSetup_HandleOscillationTable)
		end if

		//Boundaries: Left & Bottom
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			temp0 = object[currentPlayer].collisionLeft
			temp0 <<= 16
			temp0 += object[currentPlayer].xpos
			temp1 = stage.curXBoundary1
			temp1 <<= 16
			if temp0 < temp1
				if object[currentPlayer].right == true
					object[currentPlayer].xvel 	= 0x10000
					object[currentPlayer].speed = 0x10000
				else
					object[currentPlayer].xvel 	= 0
					object[currentPlayer].speed = 0
				end if

				object[currentPlayer].xpos = temp1
				temp0 = object[currentPlayer].collisionLeft
				temp0 <<= 16
				object[currentPlayer].xpos -= temp0
			end if

			temp1 = stage.curYBoundary2
			temp1 <<= 16
			if temp1 < stage.deathBoundary
				if object[currentPlayer].ypos > stage.deathBoundary
					CallFunction(PlayerObject_Kill)
				end if
			else
				if object[currentPlayer].ypos > temp1
					CallFunction(PlayerObject_Kill)
				end if
			end if
		next
	end switch

		// Sort players, player 1 should always be on top of player 2
		currentPlayer = playerCount
		currentPlayer--
		while currentPlayer > -1
			if player[currentPlayer].visible == true
				currentPlayer += playerCount
				if player[currentPlayer].sortedDrawOrder == 0
					currentPlayer -= playerCount
					arrayPos0 = player[currentPlayer].sortedDrawOrder
					AddDrawListEntityRef(arrayPos0, currentPlayer)
	
					currentPlayer += playerCount
					AddDrawListEntityRef(arrayPos0, currentPlayer)
					currentPlayer -= playerCount
				else
					currentPlayer -= playerCount
					arrayPos0 = player[currentPlayer].sortedDrawOrder
					currentPlayer += playerCount
					AddDrawListEntityRef(arrayPos0, currentPlayer)
	
					currentPlayer -= playerCount
					arrayPos0 = player[currentPlayer].sortedDrawOrder
					AddDrawListEntityRef(arrayPos0, currentPlayer)
				end if
			end if
			currentPlayer--
		loop

	// Handle Touch Controls
	if options.attractMode == false
		if object[0].controlMode > -1
			options.touchControls = true
		else
			options.touchControls = false
		end if
	else
		options.touchControls = false
	end if
end event

event ObjectDraw
	if stage.state == STAGE_PAUSED
		temp0 = screen.xsize
		temp0 /= 2
		temp1 = screen.ysize
		temp1 /= 2
		temp2 = temp0
		temp2 -= 36
		temp3 = temp1
		temp3 -= 18
		DrawRect(temp2, temp3, 73, 37, 0xE0, 0x50, 0x60, 0x40)
		DrawSpriteScreenXY(0, temp0, temp1)
		temp2 = 1
		if object.value0 == 0
			temp2++
		end if
		DrawSpriteScreenXY(temp2, temp0, temp1)
		temp2 = 3
		if object.value0 == 1
			temp2++
		end if
		DrawSpriteScreenXY(temp2, temp0, temp1)
		temp2 = 5
		if object.value0 == 2
			temp2++
		end if
		DrawSpriteScreenXY(temp2, temp0, temp1)
		DrawRect(0, 0, screen.xsize, screen.ysize, 0, 0, 0, object.value2)
	end if
end event

event ObjectStartup
	SetMusicTrack("ActComplete.ogg", 1, false)
	SetMusicTrack("Invincibility.ogg", 2, 38679)
	SetMusicTrack("Continue.ogg", 3, false)
	SetMusicTrack("Boss.ogg", 4, true)
	SetMusicTrack("GameOver.ogg", 5, false)
	SetMusicTrack("Drowning.ogg", 6, false)
	SetMusicTrack("Super.ogg", 7, true)

	stage.timeOver = false

	if options.attractMode == false
		// There is code here on the official PC version...
		// however! we don't have that!
	else
		//Set Object Range, changes internal bounds so attract mode always looks the same
		SetObjectRange(424)
	end if

	SpeedUpMusic 	= 0
	SlowDownMusic 	= 0
	stage.musicFlag = false

	stage.deathBoundary = stage.curYBoundary2
	stage.deathBoundary <<= 16

	foreach (TypeName[Stage Setup], arrayPos0, ALL_ENTITIES)
		ResetObjectEntity(arrayPos0, TypeName[Blank Object], 0, 0, 0)
	next
	object[8].type = TypeName[Stage Setup]
	object[8].priority = PRIORITY_ACTIVE_PAUSED
	object[8].drawOrder = 6

	foreach (TypeName[HUD], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].type = TypeName[Blank Object]
		if credits.screen == 0
			object[9].type 			= TypeName[HUD]
			object[9].priority 		= PRIORITY_ACTIVE
			object[9].drawOrder 	= 6
			object[9].propertyValue = object[arrayPos0].propertyValue
		end if
	next

	ringExtraLife 						= 100
	oscillation 						= 0
	StageSetup_oscillateFlipFlags 		= 0b1011111000000000

	temp0 = 0
	while temp0 < 32
		GetTableValue(temp1, temp0, StageSetup_initOscillationTable)
		SetTableValue(temp1, temp0, StageSetup_oscillationTable)
		temp0++
	loop

	if options.saveSlot > 0
		options.shieldType = 0
	end if

	blueShieldType = TypeName[Blue Shield]
	invincibilityType = TypeName[Invincibility]

	LoadSpriteSheet("Global/Display.gif")

	player.nodraw = 0
	stage.ogXBoundary = stage.curXBoundary2

	// Pause menu
	SpriteFrame(-40, -22, 81, 45, 137, 413)	// 0  - Frame
	SpriteFrame(-32, -14, 64, 7, 137, 389)	// 1  - Continue
	SpriteFrame(-32, -14, 64, 7, 170, 405)	// 2  - Continue (Sel)
	SpriteFrame(-28, -3, 56, 7, 137, 397)	// 3  - Restart
	SpriteFrame(-28, -3, 56, 7, 194, 397)	// 4  - Restart (Sel)
	SpriteFrame(-16, 8, 32, 7, 137, 405)	// 5  - Exit
	SpriteFrame(-16, 8, 32, 7, 202, 389)	// 6  - Exit (Sel)

end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event