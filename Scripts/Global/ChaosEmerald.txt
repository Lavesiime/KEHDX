// ----------------------------------
// RSDK Project: Knuckles' Emerald Hunt
// Script Description: Chaos Emerald Object
// Script Author: MainMemory
// ----------------------------------
event ObjectMain
	switch object.state
	case 1
		object.ypos = object.value0
		Sin(temp0, object.angle)
		temp0 <<= 10
		object.ypos += temp0
		object.angle += 4
		object.angle &= 511

		object.frame++
		object.frame %= 96

		// Check interaction with players
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if HPZCheck == true
				BoxCollisionTest(C_TOUCH, object.entityPos, -8, -16, 8, 16, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			else
				BoxCollisionTest(C_TOUCH, object.entityPos, -8, -8, 8, 8, currentPlayer, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO, HITBOX_AUTO)
			end if
			if checkResult == true
				CallFunction(EmeraldManager_AwardEmerald)
				ResetObjectEntity(object.entityPos, TypeName[Blank Object], 0, 0, 0)
			end if
		next
		break
	case 2
		currentPlayer = object.propertyValue
		object.xpos = object[currentPlayer].xpos
		object.ypos = object[currentPlayer].ypos
		if HPZCheck == true
			object.ypos -= 2497152
		else
			object.ypos -= 2097152
		end if
		object.value0++
		if object.value0 < 150
			temp0 = object.value0
			temp0 &= 7
			switch temp0
			case 0
				object.alpha = 255
				break
			case 1
			case 7
				object.alpha = 192
				break
			case 2
			case 6
				object.alpha = 128
				break
			case 3
			case 5
				object.alpha = 64
				break
			case 4
				object.alpha = 0
				break
			endswitch
		else
			CallFunction(EmeraldManager_DeleteEmerald)
			ResetObjectEntity(object.entityPos, TypeName[Blank Object], 0, 0, 0)
		end if
		break
	endswitch
end event


event ObjectDraw
	switch object.state
	case 1
		if HPZCheck == true
			DrawSprite(9)
		else
			if stage.listpos > 9
				object.value1++
				object.value1 &= 15
				if object.value1 < 8
					DrawSprite(10)
				else
					DrawSprite(11)
				end if
			else
				if object.frame < 64
					DrawSprite(0)
				else
					temp0 = object.frame
					temp0 -= 60
					temp0 >>= 2
					DrawSprite(temp0)
				end if
			end if
		end if
		break
	case 2
		if HPZCheck == true
			DrawSpriteFX(9, FX_INK, object.xpos, object.ypos)
		else
			if stage.listpos > 9
				DrawSpriteFX(10, FX_INK, object.xpos, object.ypos)
			else
				DrawSpriteFX(0, FX_INK, object.xpos, object.ypos)
			end if
		end if
		break
	endswitch
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items.gif")
	SpriteFrame(-8, -8, 16, 16, 109, 158)
	temp0 = 175
	while temp0 < 193
		temp1 = 109
		while temp1 < 161
			SpriteFrame(-8, -8, 16, 16, temp1, temp0)
			temp1 += 17
		loop
		temp0 += 17
	loop

	SpriteFrame(-8, -16, 16, 32, 178, 159) // 9 HPZ Shard

	temp1 = 126
	temp0 = 0
	while temp0 < 2
		SpriteFrame(-8, -8, 16, 16, temp1, 158) // S1 Emerald
		temp1 += 17
		temp0++
	loop

	foreach (TypeName[Chaos Emerald], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].drawOrder = 4
		object[arrayPos0].value0 = object[arrayPos0].ypos
		CallFunction(EmeraldManager_AddEntity)
	next
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)
end event
