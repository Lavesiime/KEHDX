// ----------------------------------
// RSDK Project: Sonic 1/Sonic 2
// Script Description: Sega Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Aliases
private alias object.value0 : KEH.timer
private alias object.value1 : KEH.menusel

private alias object.value2 : KEH.usingTouch

// Padded out for parity with other Menu objects, so that the value slot is the same across each
private alias object.value10 : KEH.buttonLeft
private alias object.value11 : KEH.buttonTop
private alias object.value12 : KEH.buttonRight
private alias object.value13 : KEH.buttonBottom
private alias object.value14 : KEH.buttonID
private alias object.value15 : KEH.buttonFunc

// Kept public as it's used by the KEH_RunButton function, which is used by other objects too
public alias 255 : MENUSEL_NULL

// States
private alias 0 : MAINMENU_SETUP
private alias 1 : MAINMENU_FADEIN
private alias 2 : MAINMENU_MAIN
private alias 3 : MAINMENU_STAGESELECT
private alias 4 : MAINMENU_OPTIONS

// Function declarations
reserve function KEH_RunButton

reserve function KEH_Button_Singleplayer
reserve function KEH_Button_Competition
reserve function KEH_Button_Options
reserve function KEH_Button_Credits


function KEH_RunButton
	
	// This function is called to update a button
	
	// Preconditions:
	// - temp3 is if the screen is held at all or not (-1, 0, or above)
	// - KEH.buttonLeft is the XPos of the left bounds of the button
	// - KEH.buttonRight is the XPos of the right bounds of the button
	// - KEH.buttonTop is the YPos of the top bounds of the button
	// - KEH.buttonBottom is the YPos of the lower bounds of the button
	// - KEH.buttonID is the button ID of the button
	// - KEH.buttonFunc is the function ID of the button, to be called if the button was pressed
	
	CheckTouchRect(KEH.buttonLeft, KEH.buttonTop, KEH.buttonRight, KEH.buttonBottom)
		
	if checkResult > -1
		KEH.menusel = KEH.buttonID
	else
		if temp3 < 0
			if KEH.menusel == KEH.buttonID
				CallFunction(KEH.buttonFunc)
			end if
		else
			if KEH.menusel == KEH.buttonID
				KEH.menusel = MENUSEL_NULL
			end if
		end if
	end if
	
end function


function KEH_Button_Singleplayer
	object.state = MAINMENU_STAGESELECT
	saveRAM[250] = KEH.menusel
end function


function KEH_Button_Competition
	PlaySfx(SfxName[Fail], false)
end function


function KEH_Button_Options
	object.state = MAINMENU_OPTIONS
	saveRAM[250] = KEH.menusel
end function


function KEH_Button_Credits
	PlaySfx(SfxName[Fail], false)
end function


event ObjectMain
	switch object.state
	case MAINMENU_SETUP
		KEH.menusel = saveRAM[250]
		KEH.timer = 320
		KEH.usingTouch = false
		
		// Set BG speed, 1.0 pixel per frame
		tileLayer[1].scrollSpeed = -0x10000
		hParallax[0].scrollSpeed = -0x10000
		
		SetActivePalette(1, 0, screen.ysize)
		SetScreenFade(0, 0, 0, KEH.timer)
		object.state++
		break

	case MAINMENU_FADEIN
		if KEH.timer > 0
			KEH.timer -= 8
		else
			PlayMusic(1)
			object.state++
		end if

		SetScreenFade(0, 0, 0, KEH.timer)
		break
		
	case MAINMENU_MAIN
		if KEH.usingTouch == false
			// Assuming physical controls, this is the default
			
			if inputPress.up == true
				KEH.menusel--
				PlaySfx(SfxName[Score Add], false)
			end if
			
			if inputPress.down == true
				KEH.menusel++
				PlaySfx(SfxName[Score Add], false)
			end if
			
			KEH.menusel &= 3
			
			if inputPress.start == true
				temp0 = KEH_Button_Singleplayer
				temp0 += KEH.menusel
				CallFunction(temp0)
			end if
			
			// If the player's touched the screen at all...
			CheckTouchRect(0, 0, screen.xsize, screen.ysize)
			
			if checkResult > -1
				// ... then activate touch controls, and hide the Knuckles cursor too
				KEH.usingTouch = true
				KEH.menusel = MENUSEL_NULL
			end if
		else
			// Seeing if the player's got a finger on the screen at all
			CheckTouchRect(0, 0, screen.xsize, screen.ysize)
			temp3 = checkResult
			
			// Get the base XPos of the button, center screen aligned
			
			// Left bounds
			KEH.buttonLeft = screen.xcenter
			KEH.buttonLeft -= 80
			
			// Right bounds
			KEH.buttonRight = KEH.buttonLeft
			KEH.buttonRight += 180
			
			// Other values
			KEH.buttonTop = 130
			KEH.buttonBottom = 150
			KEH.buttonFunc = KEH_Button_Singleplayer
			
			// Cycle through, and look at all four buttons
			KEH.buttonID = 0
			while KEH.buttonID < 4
				CallFunction(KEH_RunButton)
				
				KEH.buttonTop += 20
				KEH.buttonBottom += 20
				KEH.buttonID++
				KEH.buttonFunc++
			loop
			
			// If the player's tapping around, then resume physical controls
			temp0 = inputPress.up
			temp0 |= inputPress.down
			temp0 |= inputPress.left
			temp0 |= inputPress.right
			temp0 |= inputPress.start
			temp0 |= inputPress.buttonA
			temp0 |= inputPress.buttonB
			temp0 |= inputPress.buttonC
			temp0 ^= true
			KEH.usingTouch = temp0
		end if
		break
			
	case MAINMENU_STAGESELECT
		if KEH.timer < 256
			KEH.timer += 8
			music.volume -= 2
			SetScreenFade(0, 0, 0, KEH.timer)
		else
			// Odds are, one of these will cause a compiler warning - not an error - at any given time
			// This is intentional, it's a one-or-the-other scenario between two ways to set up the menu
			if TypeName[Stage Select] > 0
				ResetObjectEntity(object.entityPos, TypeName[Stage Select], 0, object.xpos, object.ypos)
			else
				stage.listPos = StageName[P - LEVEL SELECT]
				LoadStage()
			end if
			menu.bootMode = BOOTMODE_STAGESELECT
			StopMusic()
			SetScreenFade(0, 0, 0, 0xFF)
		end if
		break
		
	case MAINMENU_OPTIONS
		if KEH.timer < 256
			KEH.timer += 8
			music.volume -= 2
			SetScreenFade(0, 0, 0, KEH.timer)
		else
			menu.bootMode = BOOTMODE_OPTIONS
			ResetObjectEntity(object.entityPos, TypeName[Options], 0, object.xpos, object.ypos)
			StopMusic()
			SetScreenFade(0, 0, 0, 0xFF)
		end if
		break
		
	end switch
	
	// Animation for the Knuckles cursor
	object.animationTimer++
	object.animationTimer %= 60
end event


event ObjectDraw
	
	// Logo and Selections
	DrawSpriteScreenXY(0, screen.xcenter, 16)
	DrawSpriteScreenXY(1, screen.xcenter, 133)

	// Little Knuckles
	
	temp0 = KEH.menusel
	temp0 *= 20
	temp0 += 132
	
	temp1 = screen.xcenter
	temp1 -= 109
	
	object.frame = object.animationTimer
	object.frame >>= 5
	object.frame += 2
	DrawSpriteScreenXY(object.frame, temp1, temp0)
	
	// Website link
	DrawSpriteScreenXY(4, screen.xcenter, 216)
	
end event


event ObjectStartup
	LoadSpriteSheet("KEHMenu/MainTitle.gif")
	SpriteFrame(-142, -3, 284, 113, 0, 201) // 0 - Knuckles' Emerald Hunt, DX!
	SpriteFrame(-80, 0, 159, 77, 82, 112)   // 1 - Selection texts
	SpriteFrame(0, 0, 23, 22, 33, 112)      // 2 - Tiny Knuckles, Frame 1
	SpriteFrame(0, 0, 24, 22, 57, 112)      // 3 - Tiny Knuckles, Frame 2
	SpriteFrame(-152, 0, 304, 10, 0, 190)   // 4 - Webpage link
	
	SetMusicTrack("SPPMenu.ogg", 1, 214596)
	LoadPalette("MainTitle.act", 1, 0, 0, 256)
	
	options.touchControls = false
	
	// No Main Menu objects are placed into a scene normally, but if they are this would make sure they're active all the time
//	foreach (TypeName[Main Menu], arrayPos0, ALL_ENTITIES)
//		object[arrayPos0].priority = PRIORITY_ACTIVE
//	next
end event

event RSDKDraw
	DrawSprite(0)
end event

event RSDKLoad
	LoadSpriteSheet("KEHMenu/MainTitle.gif")
	SpriteFrame(-142, -3, 284, 113, 0, 201)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event